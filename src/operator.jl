export
    Creation,
    create!,
    create,
    Annihilation,
    annihilate!,
    annihilate,

    ComplexVec,
    Î±,
    Î¾,

    Displacement,
    displace!,

    Squeezing,
    squeeze!

############
# aâ€  and a #
############

"""
    Creation(; dim=DIM)

Creation operator in matrix representation

``\\hat{a}^{\\dagger}``
"""
Creation(T::Type{<:Number}; dim=DIM) = diagm(-1 => sqrt.(T.(1:dim-1)))
Creation(; dim=DIM) = Creation(ComplexF64, dim=dim)

"""
    create!(state::AbstractState)

Apply creation operator on the quantum state.

# Examples
```jldoctest
julia> state = VacuumState();

julia> create!(state);

julia> vec(state) == vec(SinglePhotonState())
true
```
"""
function create!(state::StateVector{T}) where {T}
    dim = state.dim
    state.v = Creation(T, dim=dim) * state.v

    return state
end

function create!(state::StateMatrix{T}) where {T}
    dim = state.dim
    ğœ = Creation(T, dim=dim)
    state.ğ›’ = ğœ * state.ğ›’ * ğœ'

    return state
end

"""
    create(state::AbstractState)

Apply creation operator on the new instance of the quantum state.

# Examples
```jldoctest
julia> state = VacuumState();

julia> new_state = create(state);

julia> vec(state) == vec(VacuumState())
true

julia> vec(new_state) == vec(SinglePhotonState())
true
```
"""
create(state::AbstractState) = create!(copy(state))

"""
    Annihilation(; dim=DIM)

Annihilation operator in matrix representation

``\\hat{a}``
"""
Annihilation(T::Type{<:Number}; dim=DIM) = diagm(1 => sqrt.(T.(1:dim-1)))
Annihilation(; dim=DIM) = Annihilation(ComplexF64, dim=dim)

"""
    annihilate!(state::AbstractState)

Apply annihilation operator on the quantum state.

# Examples
```jldoctest
julia> state = SinglePhotonState();

julia> annihilate!(state);

julia> vec(state) == vec(VacuumState())
true
```
"""
function annihilate!(state::StateVector{T}) where {T}
    dim = state.dim
    state.v = Annihilation(T, dim=dim) * state.v

    return state
end

function annihilate!(state::StateMatrix{T}) where {T}
    dim = state.dim
    ğš = Annihilation(T, dim=dim)
    state.ğ›’ = ğš * state.ğ›’ * ğš'

    return state
end

"""
    annihilate!(state::AbstractState)

Apply annihilation operator on the new instance of the quantum state.

# Examples
```jldoctest
julia> state = SinglePhotonState();

julia> new_state = annihilate(state);

julia> vec(state) == vec(SinglePhotonState())
true

julia> vec(new_state) == vec(VacuumState())
true
```
"""
annihilate(state::AbstractState) = annihilate!(copy(state))

###########
# Î± and Î¾ #
###########

"""
    ComplexVec{T <: Real}(r::T, Î¸::T)

Vector in polar coordinate for complex plane.

``v = r e^{-i\\theta}``
"""
struct ComplexVec{T<:Real}
    r::T
    Î¸::T
end

function Base.show(io::IO, complexvec::ComplexVec{T}) where {T}
    print(io, "ComplexVec{$T}($(complexvec.r)exp(-$(complexvec.Î¸)im))")
end

z(complexvec::ComplexVec) = complexvec.r * exp(-im * complexvec.Î¸)

"""
    Î±(r::Real, Î¸::Real)

Eigenvalue of annihilation operator.

``\\hat{a} | \\alpha \\rangle = \\alpha | \\alpha \\rangle``

# Examples
```jldoctest
julia> Î±(1.5, Ï€/4)
ComplexVec{Float64}(1.5exp(-0.7853981633974483im))
```
"""
Î±(T::Type{<:Real}, r::Real, Î¸::Real) = ComplexVec{T}(T(r), T(Î¸))
Î±(r, Î¸) = Î±(Float64, r, Î¸)

"""
    Î¾(r::Real, Î¸::Real)

# Examples
```jldoctest
julia> Î¾(1.5, Ï€/4)
ComplexVec{Float64}(1.5exp(-0.7853981633974483im))
```
"""
const Î¾ = Î±

################
# displacement #
################

"""
    Displacement(Î±::ComplexVec{<:Real}; dim=DIM)

Displacement operator in matrix representation

``\\hat{D}(\\alpha) = exp(\\alpha \\hat{a}^{\\dagger} - \\alpha^{*} \\hat{a})``
"""
function Displacement(T::Type{<:Number}, Î±::ComplexVec; dim=DIM)
    return exp(z(Î±) * Creation(T, dim=dim) - z(Î±)' * Annihilation(T, dim=dim))
end

Displacement(Î±::ComplexVec; dim=DIM) = Displacement(ComplexF64, Î±, dim=dim)

"""
    displace!(state::AbstractState)

Apply displacement operator on the quantum state.

# Examples
```jldoctest
julia> state = VacuumState();

julia> displace!(state, Î±(5., Ï€/4));

julia> vec(state) == vec(CoherentState(Î±(5., Ï€/4)))
true
```
"""
function displace!(state::StateVector{T}, Î±::ComplexVec) where {T}
    dim = state.dim
    state.v = Displacement(T, Î±, dim=dim) * state.v

    return state
end

function displace!(state::StateMatrix{T}, Î±::ComplexVec) where {T}
    dim = state.dim
    ğ = Displacement(T, Î±, dim=dim)
    state.ğ›’ = ğ * state.ğ›’ * ğ'

    return state
end

#############
# squeezing #
#############

"""
    Squeezing(Î¾::ComplexVec{<:Real}; dim=DIM)

Squeezing operator in matrix representation

``\\hat{S}(\\xi) = exp(\\frac{1}{2} (\\xi^{*} \\hat{a}^{2} - \\xi \\hat{a}^{\\dagger 2}))``
"""
function Squeezing(T::Type{<:Number}, Î¾::ComplexVec; dim=DIM)
    return exp(0.5 * z(Î¾)' * Annihilation(T, dim=dim)^2 - 0.5 * z(Î¾) * Creation(T, dim=dim)^2)
end

Squeezing(Î¾::ComplexVec; dim=DIM) = Squeezing(ComplexF64, Î¾, dim=dim)

"""
    squeeze!(state::AbstractState)

Apply squeezing operator on the quantum state.

# Examples
```jldoctest
julia> state = VacuumState();

julia> squeeze!(state, Î±(0.5, Ï€/4));

julia> vec(state) == vec(SqueezedState(Î¾(0.5, Ï€/4)))
true
```
"""
function squeeze!(state::StateVector{T}, Î¾::ComplexVec) where {T}
    dim = state.dim
    state.v = Squeezing(T, Î¾, dim=dim) * state.v

    return state
end

function squeeze!(state::StateMatrix{T}, Î¾::ComplexVec) where {T}
    dim = state.dim
    ğ¬ = Squeezing(T, Î¾, dim=dim)
    state.ğ›’ = ğ¬ * state.ğ›’ * ğ¬'

    return state
end

###################
# BHD measurement #
###################

##### for arb. state in intensity-to-measurement-phase quadrature coordinate #####

# |Î¸, xâŸ© = âˆ‘â‚™ |nâŸ© âŸ¨n|Î¸, xâŸ© = âˆ‘â‚™ Ïˆâ‚™(Î¸, x) |nâŸ©
# âŸ¨n|Î¸, xâŸ© = Ïˆâ‚™(Î¸, x) = exp(im n Î¸) (2/Ï€)^(1/4) exp(-x^2) Hâ‚™(âˆš2 x)/âˆš(2^n n!)
function Ïˆâ‚™(n::Integer, Î¸::Real, x::Real)
    return (2/Ï€)^(1/4) * exp(im*n*Î¸ - x^2) * hermiteh(n, sqrt(2)x) / sqrt(2^n * factorial(n))
end

function ğ›‘Ì‚!(result::Matrix{<:Complex}, Î¸::Real, x::Real; dim=DIM)
    view(result, :, 1) .= Ïˆâ‚™.(big(0):big(dim-1), Î¸, x)
    result .= view(result, :, 1) * view(result, :, 1)'

    return result
end

function ğ›‘Ì‚(T::Type{<:Complex}, Î¸::Real, x::Real; dim=DIM)
    result = Matrix{T}(undef, dim, dim)

    return ğ›‘Ì‚!(result, Î¸, x, dim=dim)
end

ğ›‘Ì‚(Î¸::Real, x::Real; dim=DIM) = ğ›‘Ì‚(ComplexF64, Î¸, x, dim=dim)

##### for Gaussian state in intensity-to-measurement-phase quadrature coordinate #####

# Ï€Ì‚â‚“ = (aÌ‚ exp(-im Î¸) + aÌ‚â€  exp(im Î¸)) / 2

tr_mul(ğš, ğ›) = sum(ğš[i, :]' * ğ›[:, i] for i in 1:size(ğš, 1))
create_Î¼(state::StateMatrix{T}) where {T} = tr_mul(Creation(T, dim=state.dim), state.ğ›’)
createÂ²_Î¼(state::StateMatrix{T}) where {T} = tr_mul(Creation(T, dim=state.dim)^2, state.ğ›’)
annihilate_Î¼(state::StateMatrix{T}) where {T} = tr_mul(Annihilation(T, dim=state.dim), state.ğ›’)
annihilateÂ²_Î¼(state::StateMatrix{T}) where {T} = tr_mul(Annihilation(T, dim=state.dim)^2, state.ğ›’)
create_annihilate_Î¼(state::StateMatrix{T}) where {T} = tr_mul(
    Creation(T, dim=state.dim) * Annihilation(T, dim=state.dim),
    state.ğ›’
)

# âŸ¨Ï€Ì‚â‚“Â²âŸ© = âŸ¨aÌ‚aÌ‚ exp(-2im Î¸) + aÌ‚â€ aÌ‚â€  exp(2im Î¸) + aÌ‚aÌ‚â€  + aÌ‚â€ aÌ‚âŸ© / 4
# âŸ¨Ï€Ì‚â‚“Â²âŸ© = (exp(-2im Î¸)âŸ¨aÌ‚Â²âŸ© + exp(2im Î¸)âŸ¨aÌ‚â€ Â²âŸ© + 1 + 2âŸ¨aÌ‚aÌ‚â€ âŸ©) / 4
# here, âŸ¨aÌ‚aÌ‚â€  + aÌ‚â€ aÌ‚âŸ© = 1 + 2âŸ¨aÌ‚aÌ‚â€ âŸ© due to the commutation relation
function Ï€Ì‚â‚“Â²_Î¼(Î¸s::AbstractVector{<:Number}, state::StateMatrix)
    return (
        exp.(-2im*Î¸s) .* annihilateÂ²_Î¼(state) .+
        exp.(2im*Î¸s) .* createÂ²_Î¼(state) .+
        1 .+ 2create_annihilate_Î¼(state)
    ) ./ 4
end

# âŸ¨Ï€Ì‚â‚“âŸ© = âŸ¨aÌ‚ exp(-im Î¸) + aÌ‚â€  exp(im Î¸)âŸ© / 2
# âŸ¨Ï€Ì‚â‚“âŸ© = (exp(-im Î¸)âŸ¨aÌ‚âŸ© + exp(im Î¸)âŸ¨aÌ‚â€ âŸ©) / 2
function Ï€Ì‚â‚“_Î¼(Î¸s::AbstractVector{<:Number}, state::StateMatrix)
    return (
        exp.(-im*Î¸s) .* annihilate_Î¼(state) .+
        exp.(im*Î¸s) .* create_Î¼(state)
    ) ./ 2
end
